EBNF 完整版

module = {block}

block = functionBlock

functionBlock = "fn" identifier [typeParams] "(" parameters ")" type "{" {statement} "}"

typeParams = "[" identifier  {"," identifier}  "]"

parameters = e | varNameType {"," varNameType}

varNameType = identifier ":" type

primaryExpr = identifier | constant | '(' expression ')'

postfixExpr = 
    primaryExpr | 
    postfixExpr '(' arguments ')' |
    postfixExpr '.' identifier

unaryExpr = postfixExpr | unary_op unaryExpr

binaryExpr = unaryExpr | binaryExpr binaryOp unaryExpr

expression = binaryExpr | unaryExpr assignOp expression 

arguments = e | expression {"," expression}

varList  = e | identifier {"," identifier}

varDefStmt = ("var"|"val") varList [":" type] ["=" expression] ";"

singleStmt = expression ";"

returnStmt = "return" expression ";"

ifStmt = "if" expression "{" {statement} "}" ["else" "{" {statement} "}"]

forStmt = "for" identifier "in" expression "{" {statement} "}"

whileStmt = ("while" expression | "loop") "{" {statement} "}"

statement = singleStmt | varDefStmt | returnStmt | ifStmt | forStmt | whileStmt

productType = "(" [type | varNameType] {"," (type | varNameType)} ")"

sumType = varNameType {"|" varNameType}

type = identifier | productType | sumType

typeDef = "data" identifier "=" type